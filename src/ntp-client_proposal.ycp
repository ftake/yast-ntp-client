/**
 * File:	clients/ntp-client_proposal.ycp
 * Summary:	Installation client for ntp configuration
 * Author:	Bubli <kmachalkova@suse.cz>
 *
 * This is used as the general interface between yast2-country
 * (time,timezone) and yast2-ntp-client.
 */

{
textdomain "ntp-client";


import "Address";
import "NetworkService";
import "NtpClient";
import "Service";
import "String";
import "Stage";
import "PackageSystem";
import "Popup";
import "Progress";
import "Report";

y2milestone("----------------------------------------");
y2milestone("Ntp client proposal started");
y2milestone("Arguments: %1", WFM::Args());

/*
    API:

Usual *_proposal functions: MakeProposal, AskUser, Write.
(but not Description; see, it just *looks* like *_proposal)
Additionally:
 CreateUI	(thick and fragile interface, grr)
 GetNTPEnabled  (queries Service::Enabled)
 GetUseNTP      (queries NtpClient::ntp_selected, just a storage for the caller)
 SetUseNTP [ntp_used]
*/

any ret = nil;
string func = "";
map param = $[];

if(size(WFM::Args()) > 0 && is(WFM::Args(0), string)) {
    func = (string)WFM::Args(0);
    if(size(WFM::Args()) > 1 && is(WFM::Args(1), map))
	param = (map)WFM::Args(1);
}

void ProposeSomething()
{
   list ntp_items = [];

   //on the running system, read all the data, otherwise firewall
   //and other stuff outside ntp.conf may not be initialized correctly
   //(#375877) 
   if (!Stage::initial())
   {
      boolean progress_orig = Progress::set (false);
      NtpClient::Read();
      Progress::set (progress_orig);
   }
   // ntp_selected is true if NTP was proposed during installation (fate#303520)
   else if (!NtpClient::ntp_selected)
      NtpClient::ProcessNtpConf();

   if( NtpClient::config_has_been_read || NtpClient::ntp_selected)
   {
	// grr, GUNS means all of them are used and here we just pick one
	ntp_items = maplist(string server, NtpClient::GetUsedNtpServers(),{
	   return `item(`id(server), server); 
	});
	// avoid calling Read again (bnc #427712)
	NtpClient::config_has_been_read = true;
   }
   if( ntp_items == [])
   {
        string cc = param["country"]:NtpClient::GetCurrentLanguageCode();
	y2milestone("Nothing found in /etc/ntp.conf, proposing current language-based NTP server list");
        ntp_items = NtpClient::GetNtpServersByCountry( cc, true );
	NtpClient::config_has_been_read = true;
    }
    ntp_items = add(ntp_items, "");
    UI::ChangeWidget(`id(`ntp_address), `Items, ntp_items);

}

void AddSingleServer( string server )
{

   integer idx = NtpClient::findSyncRecord("server", server); 

   // -1 means adding new server
   if (idx == -1)
   {
        NtpClient::selected_record["address"] = server;
        NtpClient::selected_record["type"] = "server";
   }
   else
	NtpClient::selectSyncRecord(idx);

   NtpClient::storeSyncRecord();
}

boolean ValidateSingleServer( string ntp_server )
{
    if (!Address::Check(ntp_server) )
    {
        UI::SetFocus(`id(`ntp_address));
	return false;
    }

    return true;
}

if (func == "CreateUI")
{
   term cont = `VBox (`VSpacing (0.5), `HBox (
	`HSpacing (3),
	`HWeight (1, `VBox (
            `Left (`ComboBox (`id (`ntp_address), `opt (`editable, `hstretch),
                // combo box label
                _("NTP Server Address")
	    )),
	    `VSpacing (0.3),
	    `HBox (
		`HSpacing (0.5),
		// check box label
		`Left (`CheckBox (`id (`ntp_save), _("Save NTP Configuration")))
	    )
	)),
	`HWeight (1, `VBox (
            `Label (""),
	    `VSpacing (0.3), // try to line up the widgets horizontally
            // push button label
            `Left (`PushButton (`id (`ntp_now), _("Synchronize now"))),
	    `VSpacing (0.3),
            // push button label
	    // bnc#449615: only simple config for inst-sys
            Stage::initial() ? `Label("") : `Left (`PushButton (`id (`ntp_configure), _("Configure...")))
	))
    ));

    if (UI::WidgetExists(`id(`rp)))
    {
        UI::ReplaceWidget(`id(`rp), cont);

        if (!NetworkService::isNetworkRunning())
        {
            y2warning("Network is not running, NTP synchronization will not be available");
            UI::ChangeWidget(`id(`ntp_content), `Enabled, false);
        }
    }

    ret = true;
}

else if (func == "GetNTPEnabled")
{
    ret	= Service::Enabled (NtpClient::service_name);
}


else if (func == "GetUseNTP")
{
    /*
    if (param["first_time"]:false && !Stage::initial ())
    {
	ret	= Service::Enabled (NtpClient::service_name);
    }
    else
    */
	ret = NtpClient::ntp_selected;
}

else if (func == "SetUseNTP")
{
    NtpClient::ntp_selected = param["ntp_used"]:false;
    ret = true;
}

else if (func == "MakeProposal")
{
    ProposeSomething();
    ret = true;
}

/*
params:
  server (taken from UI if empty)
  servers (intended to use all of opensuse.pool.ntp.org,
	   but I did not have time to make it work)
  ntpdate_only (TODO rename to onetime)
*/
else if (func == "Write")
{
    string ntp_server	= param["server"]:"";
    if (ntp_server == "")
	// get the value from UI only when it wasn't given as a parameter
	ntp_server	= (string) UI::QueryWidget(`id(`ntp_address), `Value);

    // One-time adjusment without running the ntp daemon
    // Meanwhile, ntpdate was replaced by sntp
    boolean ntpdate_only = param["ntpdate_only"]:false;

    if ( !ValidateSingleServer( ntp_server ) )
	ret = `invalid_hostname;
    else
    {
	string required_package = "ntp";

        //In 1st stage, schedule packages for installation
        //but not in case user wants to set the time only (F#302917)
	//(ntpdate is in inst-sys so we don't need the package)
        if (Stage::initial() && !ntpdate_only)
	{
		import "Packages";
	        Packages::addAdditionalPackage(required_package);
	        // bugzilla #327050
	        // Agent for writing /etc/ntp.conf needs to be installed
	        // to write the settings at the end of the installation
	        Packages::addAdditionalPackage("yast2-ntp-client");
	}
	//Otherwise, prompt user for confirming pkg installation
	else if (!Stage::initial ())
	{
	   if (!PackageSystem::CheckAndInstallPackages([ required_package ]))
	   {
	        Report::Error(sformat( _("Synchronization with NTP server is not possible
without having package %1 installed"), required_package));
	   }
	}

        Popup::ShowFeedback("", _("Synchronizing with NTP server..."));

        y2milestone("Running sntp to sync with %1", ntp_server);

	// -r: do set the system time
	// -P no: do not ask if time difference is too large
	// -c 1 -d 15: delay 15s, only one try (bnc#442287)
        integer r = (integer) SCR::Execute (.target.bash, sformat ("/usr/sbin/sntp -c 1 -d 15 -r -P no '%1'", String::Quote(ntp_server)));
        y2milestone ("'sntp %1' returned %2", ntp_server, r);

	Popup::ClearFeedback();

        if (r == 0)
        {
            ret = `success;

	    // User wants to more than running sntp (synchronize on boot)
	    if (!ntpdate_only)
            {
	        NtpClient::modified = true;
	        AddSingleServer(ntp_server);

	        //OK, so we stored the server address
	        //In inst-sys we don't need to care further
	        //ntp-client_finish will do the job
	        //In installed system we must write the settings
	        if (!Stage::initial())
		    NtpClient::Write();
	    }
        }
        else
	    ret = `ntpdate_failed;
    }
}

else if (func == "AskUser")
{
    string ntp_server = (string) UI::QueryWidget(`id(`ntp_address), `Value);
    if ( !ValidateSingleServer( ntp_server ) )
	ret = `invalid_hostname;
    else
    {
        string ntp_server = (string) UI::QueryWidget(`id(`ntp_address), `Value);
        AddSingleServer(ntp_server);
        boolean retval = (boolean) WFM::CallFunction("ntp-client");
	if (retval)
	    ret	= `next;
        ProposeSomething();
    }
}

y2milestone("Ntp client proposal finished");
y2milestone("----------------------------------------");
return ret;
}


