/**
 * File:	include/ntp-client/wizards.ycp
 * Package:	Configuration of ntp-client
 * Summary:	Wizards definitions
 * Authors:	Jiri Srain <jsrain@suse.cz>
 *
 * $Id$
 */

{

textdomain "ntp-client";

import "Wizard";
import "Label";

include "wizard/sequencer.ycp";

include "ntp-client/dialogs.ycp";



/**
 * Complex workflow of the ntp-client configuration
 * @return sequence result
 */
define symbol ComplexSequence() ``{

    map aliases = $[
	"main"		:   ``(MainDialog ()),
	"type_select"	:   ``(TypeSelectDialog ()),
	"server"	:   ``(ServerDialog ()),
	"peer"		:   ``(PeerDialog ()),
	"clock"		:   ``(RadioDialog ()),
	"fudge"		:   ``(FudgeDialog ()),
	"bcast"		:   ``(BCastDialog ()),
	"bcastclient"	:   ``(BCastClientDialog ()),
	"firewall"	:   ``(FirewallDialog ()),
	"store_record"	: [ ``(StoreRecord ()), true],
    ];

    map sequence = $[
	"ws_start" : "main",
	"main" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `peer	: "peer",
	    `server	: "server",
	    `clock	: "clock",
	    `bcast	: "bcast",
	    `bcastclient: "bcastclient",
	    `add	: "type_select",
	    `firewall	: "firewall",
	],
	"firewall" : $[
	    `abort	: `abort,
	    `next	: "main",
	],
	"type_select" : $[
	    `abort	: `abort,
	    `peer	: "peer",
	    `server	: "server",
	    `clock	: "clock",
	    `bcast	: "bcast",
	    `bcastclient: "bcastclient",
	],
	"peer" : $[
	    `abort	: `abort,
	    `next	: "store_record",
	],
	"server" : $[
	    `abort	: `abort,
	    `next	: "store_record",
	],
	"clock" : $[
	    `abort	: `abort,
	    `next	: "store_record",
	    `fudge	: "fudge"
	],
	"fudge" : $[
	    `abort	: `abort,
	    `next	: "clock",
	],
	"bcast" : $[
	    `abort	: `abort,
	    `next	: "store_record",
	],
	"bcastclient" : $[
	    `abort	: `abort,
	    `next	: "store_record",
	],
	"store_record" : $[
	    `abort	: `abort,
	    `next	: "main",
	],
    ];

    symbol ret = WizardSequencer(aliases, sequence);

    return ret;
}

/**
  * The simple workflow for the NTP client
  * @return sequence result
  */
define symbol SimpleSequence () ``{

    NtpClient::selected_record = $[
	"type" : "__clock",
	"address" : "127.127.3.2",
    ];

    map aliases = $[
	"switcher"	:[``(SelectConfigType ()), true ],
	"simple"	: ``(SimpleDialog ()),
	"complex"	: ``(ComplexSequence ()),
    ];

    map sequence = $[
	"ws_start" : "switcher",
	"switcher" : $[
	    `simple : "simple",
	    `complex : "complex",
	],
	"simple" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `complex	: "complex",
	],
	"complex" : $[
	    `abort	: `abort,
	    `next	: `next,
	],
    ];

    symbol ret = WizardSequencer (aliases, sequence);

    return ret;
}

/**
 * Whole configuration of ntp-client
 * @return sequence result
 */
define symbol NtpClientSequence() ``{

    map aliases = $[
	"read"	: [ ``( ReadDialog() ), true ],
	"main"	:   ``( SimpleSequence() ),
	"write"	: [ ``( WriteDialog() ), true ]
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" : $[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    Wizard::CreateDialog();
    symbol ret = WizardSequencer(aliases, sequence);

    UI::CloseDialog();
    return ret;
}

/**
 * Autoyast configuration of ntp-client
 * @return sequence result
 */
define symbol NtpClientAutoSequence() ``{

    map aliases = $[
	"main"	:   ``( ComplexSequence() )
    ];

    map sequence = $[
	"ws_start" : "main",
	"main" : $[
	    `abort  : `abort,
	    `next   : `next
	]
    ];

    // dialog caption
    string caption = _("NTP Client Configuration");
    // label
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    Label::BackButton(), Label::NextButton());

    symbol ret = WizardSequencer(aliases, sequence);

    UI::CloseDialog();
    return ret;
}
/* EOF */
}
