/**
 * File:	include/ntp-client/widgets.ycp
 * Package:	Configuration of ntp-client
 * Summary:	Widgets definitions
 * Authors:	Jiri Srain <jsrain@suse.cz>
 *
 * $Id$
 */

{

textdomain "ntp-client";

import "Popup";
import "Label";
import "LogView";
import "SLP";

include "ntp-client/helps.ycp";

// selected type of peer
string peer_type_selected = nil;

/**
 * Show popup with NTP daemon's log
 */
define void showLogPopup () ``{
    LogView::Display ($[
	"file" : "/var/log/ntp",
	"save" : true,
	"actions" : [
	    // menubutton entry, try to keep short
	    [ _("Restart DNS Server"),
		restartNtpDaemon ],
	    // menubutton entry, try to keep short
	    [ _("Save Settings and Restart DNS Server"),
		NtpClient::Write ],
	],
    ]);
    return nil;
}

/**
 * Handle function of the widget
 * @param id string widget id
 * @param event map event that caused storing process
 * @return symbol always nil
 */
global define symbol ntpEnabledOrDisabled (string id, map event) ``{
    any ev_id = event["ID"]:nil;
    if (ev_id == "boot" || ev_id == "never")
    {
	boolean enabled = UI::QueryWidget (`id ("start"), `CurrentButton)
	    != "never";
	UI::ChangeWidget (`id (id), `Enabled, enabled);
    }
}

/**
 * Initialize the widget
 * @param id any widget id
 */
global define void startInit (any id) ``{
    UI::ChangeWidget (`id ("start"), `CurrentButton,
	run_service ? "boot" : "never");
}

/**
 * Store settings of the widget
 * @param id any widget id
 * @param event map event that caused storing process
 */
global define void startStore (any id, map event) ``{
    run_service = UI::QueryWidget (`id ("start"), `CurrentButton) == "boot";
}

/**
 * Handle function of the widget
 * @param id string widget id
 * @param event map event that caused storing process
 * @return any always nil
 */
global define any startHandle (string id, map event) ``{
    boolean start = UI::QueryWidget (`id ("start"), `CurrentButton) == "boot";
    list<string> devices = NetworkDevices::Locate ("STARTMODE", "onboot");
    devices = filter (string d, devices, ``(d != "lo"));
    // yes-no popup
    if (start && size (devices) == 0 && ! Popup::ContinueCancel (_("Warning!

If you do not have a permanent Internet connection,
starting the NTP daemon can take a very long time and 
the daemon might not run properly.")))
	UI::ChangeWidget (`id ("start"), `CurrentButton, "never");

    if (start != run_service)
	NtpClient::modified = true;

    return nil;
}

/**
 * Handle function of the widget
 * @param key any widget id
 * @param event map event that caused storing process
 * @return symbol always `complex
 */
global define symbol complexButtonHandle (string key, map event) ``{
    if (event["ID"]:nil == "complex_button")
	return `complex;
    ntpEnabledOrDisabled (key, event);
    return nil;
}

/**
 * Handle function of the widget
 * @param key string widget id
 * @param event map event that caused storing process
 * @return symbol always `complex
 */
global define symbol fudgeButtonHandle (string key, map event) ``{
    return `fudge;
}

/**
 * Initialize the widget
 * @param id any widget id
 */
global define void chrootInit (any id) ``{
    UI::ChangeWidget (`id (id), `Value, NtpClient::run_chroot);
}

/**
 * Store settings of the widget
 * @param id any widget id
 * @param event map event that caused storing process
 */
global define void chrootStore (any id, map event) ``{
    NtpClient::run_chroot = (boolean)(UI::QueryWidget (`id (id), `Value));
    NtpClient::modified = true;
}

/**
 * Initialize the widget
 * @param id any widget id
 */
global define void overviewInit (any id) ``{
    map types = $[
	// table cell, NTP relationship type
	"server" : _("Server"),
	// table cell, NTP relationship type
	"peer" : _("Peer"),
	// table cell, NTP relationship type
	"broadcast" : _("Broadcast"),
	// table cell, NTP relationship type
	"broadcastclient" : _("Accepting Broadcasts"),
    ];
    list items = maplist (map<string,any> i, NtpClient::getSyncRecords (), ``{
	string type = i["type"]:"";
	string address = i["address"]:"";
	integer index = i["index"]:-1;
	if (type == "__clock")
	{
	    integer clock_type = getClockType (address);
	    integer unit_number = getClockUnitNumber (address);
	    string device = i["device"]:"";
	    if (device == "")
		// table cell, %1 is integer 0-3
		device = sformat (_("Unit Number: %1"), unit_number);
	    if (clock_type == 1 && unit_number == 0)
		device = "";
	    string clock_name = clock_types[clock_type, "name"]:"";
	    if (clock_name == "")
		// table cell, NTP relationship type
		clock_name = _("Local Radio Clock");
	    return `item (`id (index), clock_name, device);
	}
	return `item (`id (index), types[type]:"", address);
    });
    UI::ChangeWidget (`id (`overview), `Items, items);
    UI::SetFocus (`id (`overview));
}

/**
 * Handle events on the widget
 * @param id any widget id
 * @param event map event that caused storing process
 * @return any event to pass to WS or nil
 */
global define any overviewHandle (string id, map event) ``{
//    ntpEnabledOrDisabled (id, event);
    if (event["ID"]:nil == `display_log)
    {
	showLogPopup ();
	return nil;
    }
    if (event["ID"]:nil == `firewall)
    {
	return `firewall;
    }
    NtpClient::modified = true;
    any ev_id = event["ID"]:nil;
    if (ev_id == "boot" || ev_id == "never")
    {
	boolean enabled = UI::QueryWidget (`id ("start"), `CurrentButton)
	    != "never";
	UI::ChangeWidget (`id (`add), `Enabled, enabled);
	UI::ChangeWidget (`id (`edit), `Enabled, enabled);
	UI::ChangeWidget (`id (`delete), `Enabled, enabled);
	UI::ChangeWidget (`id (`overview), `Enabled, enabled);
	UI::ChangeWidget (`id (`advanced), `Enabled, enabled);
	return nil;
    }
    map types = $[
	"server" : `server,
	"peer" : `peer,
	"__clock" : `clock,
	"broadcast" : `bcast,
	"broadcastclient" : `bcastclient,
    ];
    if (event["ID"]:nil == `add)
    {
	NtpClient::selectSyncRecord (-1);
	peer_type_selected = nil;
	return `add;
    }
    else if (event["ID"]:nil == `edit || event["ID"]:nil == `overview)
    {
	NtpClient::selectSyncRecord (
	    (integer)(UI::QueryWidget (`id (`overview), `CurrentItem))
	);
	string type = selected_record["type"]:"";
	return types[type]:nil;
    }
    else if (event["ID"]:nil == `delete)
    {
	// yes-no popup
	if (Popup::YesNo (_("Delete the selected item?")))
	{
	    NtpClient::deleteSyncRecord (
		(integer)(UI::QueryWidget (`id (`overview), `CurrentItem))
	    );
	    overviewInit (id);
	}
    }

}

/**
 * Initialize the widget
 * @param id any widget id
 */
global define void addressInit (any id) ``{
    UI::ChangeWidget (`id (id), `Value,
	NtpClient::selected_record["address"]:"");
    UI::SetFocus (`id (id));
}

/**
 * Store settings of the widget
 * @param id any widget id
 * @param event map event that caused storing process
 */
global define void addressStore (any id, map event) ``{
    NtpClient::selected_record["address"] = UI::QueryWidget (`id (id), `Value);
    if (NtpClient::simple_dialog)
    {
	selected_record["type"] = "server";
	selected_record["initial_sync"] = true;
    }
}

/**
 * Handle events on the widget
 * @param id string widget id
 * @param event map event that caused storing process
 * @return symbol event to pass to WS or nil
 */
global define symbol serverAddressHandle (string id, map event) {
    any ev_id = event["ID"]:nil;
    if (ev_id == "boot" || ev_id == "never")
    {
	boolean enabled = UI::QueryWidget (`id ("start"), `CurrentButton)
	    != "never";
	UI::ChangeWidget (`id (id), `Enabled, enabled);
	UI::ChangeWidget (`id (`lookup_address), `Enabled, enabled);
	return nil;
    }
    if (ev_id == `lookup_address)
    {
	list<map> servers = SLP::FindSrvs ("service:ntp", "");
	list<string> server_names = maplist (map m, servers, ``(
	    (string)(m["pcHost"]:"")
	));
	server_names = filter (string s, server_names, ``(s != ""));
	UI::ReplaceWidget (`server_address_rp,
	    `ComboBox (`id ("server_address"),
		`opt (`editable, `hstretch),
		// combo box label
		_("&NTP Server"),
		server_names));
	return nil;
    }
    return nil;
}

/**
 * Initialize the widget
 * @param id any widget id
 */
global define void initSyncInit (any id) ``{
    UI::ChangeWidget (`id ("init_sync"), `Value,
	selected_record["initial_sync"]:false);
}

/**
 * Store settings of the widget
 * @param id any widget id
 * @param event map event that caused storing process
 */
global define void initSyncStore (any id, map event) ``{
    selected_record["initial_sync"]
	= UI::QueryWidget (`id ("init_sync"), `Value);
}

/**
 * Initialize the widget
 * @param id any widget id
 */
global define void optionsInit (any id) ``{
    UI::ChangeWidget (`id ("options"), `Value, selected_record["param"]:"");
}

/**
 * Store settings of the widget
 * @param id any widget id
 * @param event map event that caused storing process
 */
global define void optionsStore (any id, map event) ``{
    selected_record["param"] = UI::QueryWidget (`id ("options"), `Value);
}

/**
 * Initialize the widget
 * @param id any widget id
 */
global define void peerTypesInit (any id) ``{
    if (peer_type_selected != nil)
	UI::ChangeWidget (`id ("peer_types"), `CurrentButton,
	    peer_type_selected);
    else
	UI::ChangeWidget (`id ("peer_types"), `CurrentButton, "server");
}

/**
 * Handle events on the widget
 * @param id any widget id
 * @param event map event that caused storing process
 * @return symbol event to pass to WS or nil
 */
global define symbol peerTypesHandle (any id, map event) ``{
    peer_type_selected = (string)UI::QueryWidget (`id ("peer_types"), `CurrentButton);
    map<string,symbol> types = $[
	"server" : `server,
	"peer" : `peer,
	"__clock" : `clock,
	"broadcast" : `bcast,
	"broadcastclient" : `bcastclient,
    ];
    if (event["ID"]:nil == `next)
    {
	selected_record["type"] = peer_type_selected;
	return types[peer_type_selected]:nil;
    }
    return nil;
}

/**
 * Initialize the widget
 * @param id string widget id
 */
global define void createSymlinkInit (string id) ``{
    UI::ChangeWidget (`id (id), `Value,
	selected_record["create_symlink"]:false);
}

/**
 * Handle function of the widget
 * @param id string widget id
 * @param event map event that caused storing process
 * @return symbol always nil
 */
global define symbol createSymlinkHandle (string id, map event) ``{
    boolean active = (boolean)UI::QueryWidget (`id ("create_symlink"), `Value);
    integer current = tointeger ((string)UI::QueryWidget (`id (id), `Value));
    active = active && clock_types[current, "device"]:"" == "";
    UI::ChangeWidget (`id ("device"), `Enabled, active);
    UI::ChangeWidget (`id ("browse"), `Enabled, active);
    return nil;
}

/**
 * Store settings of the widget
 * @param id string widget id
 * @param event map event that caused storing process
 */
global define void createSymlinkStore (string id, map event) ``{
    NtpClient::selected_record["create_symlink"] = (boolean)
	UI::QueryWidget (`id (id), `Value);
}

/**
 * Handle function of the widget
 * @param id string widget id
 * @param event map event that caused storing process
 * @return symbol always nil
 */
global define symbol clockTypeHandle (string id, map event) ``{
    integer current = tointeger ((string)UI::QueryWidget (`id (id), `Value));
    boolean hw_avail = clock_types[current, "device"]:"" != "";
    UI::ChangeWidget (`id ("create_symlink"), `Enabled, hw_avail);
    return createSymlinkHandle (id, event);
}

/**
 * Initialize the widget
 * @param id string widget id
 */
global define void clockTypeInit (string id) ``{
    UI::ChangeWidget (`id (id), `Value, sformat ("%1",
	getClockType (NtpClient::selected_record["address"]:"")));
    clockTypeHandle (id, $[]);
}

/**
 * Store settings of the widget
 * @param id string widget id
 * @param event map event that caused storing process
 */
global define void clockTypeStore (string id, map event) ``{
    NtpClient::selected_record["address"] = setClockType (
	NtpClient::selected_record["address"]:"",
	tointeger ((string)UI::QueryWidget (`id (id), `Value)));
}

/**
 * Initialize the widget
 * @param id string widget id
 */
global define void unitNumberInit (string id) ``{
    UI::ChangeWidget (`id (id), `Value,
	getClockUnitNumber (NtpClient::selected_record["address"]:""));
}

/**
 * Store settings of the widget
 * @param id string widget id
 * @param event map event that caused storing process
 */
global define void unitNumberStore (string id, map event) ``{
    NtpClient::selected_record["address"] = setClockUnitNumber (
	NtpClient::selected_record["address"]:"",
	(integer)UI::QueryWidget (`id (id), `Value));
}

/**
 * Handle function of the widget
 * @param id string widget id
 * @param event map event that caused storing process
 * @return symbol always nil
 */
global define symbol browseButtonHandle (string id, map event) ``{
    string current = (string)UI::QueryWidget (`id ("device"), `Value);
    if (current == "")
	current = "/dev";
    // popup header
    current = UI::AskForExistingFile (current, "", _("Select the Device"));
    if (current != nil)
	UI::ChangeWidget (`id ("device"), `Value, current);
    return nil;
}

/**
 * Initialize the widget
 * @param id string widget id
 */
global define void deviceInit (string id) ``{
    UI::ChangeWidget (`id (id), `Value, selected_record["device"]:"");
}

/**
 * Store settings of the widget
 * @param id string widget id
 * @param event map event that caused storing process
 */
global define void deviceStore (string id, map event) ``{
    NtpClient::selected_record["device"] = (string)
	UI::QueryWidget (`id (id), `Value);
}


/**
 * Initialize all widgets
 * @return a map of widgets
 */
global define map<string,any> InitWidgets () ``{
    map address = $[
	    "widget" : `textentry,
	    // text entry label
	    "label" : _("A&ddress"),
	    "init" : NtpClient::addressInit,
	    "store" : NtpClient::addressStore,
    ];
    return $[
	"complex_button" : $[
	    "widget" : `push_button,
	    // push button label
	    "label" : _("Co&mplex Configuration"),
	    "help" : HELPS["complex_button"]:"",
	    "handle_events" : [ "complex_button", "never", "boot" ],
	    "handle" : NtpClient::complexButtonHandle,
	],
	"fudge_button" : $[
	    "widget" : `push_button,
	    // push button label
	    "label" : _("&Driver Calibration"),
	    "help" : HELPS["fudge_button"]:"",
	    "handle_events" : [ "fudge_button" ],
	    "handle" : NtpClient::fudgeButtonHandle,
	],
	"start" : $[
	    "widget" : `radio_buttons,
	    // frame
	    "label" : _("Automatically Start NTP Daemon"),
	    "items" : [
		// radio button
		[ "never", _("N&ever") ],
		// radio button
		[ "boot", _("When &Booting System") ],
	    ],
	    "help" : HELPS["start"]:"",
	    "init" : NtpClient::startInit,
	    "store" : NtpClient::startStore,
	    "handle" : NtpClient::startHandle,
	    "handle_events" : [ "boot" ],
	    "opt" : [`notify],
	],
	"run_chroot" : $[
	    "widget" : `checkbox,
	    // check box
	    "label" : _("Run NTP Daemon in Chroot &Jail"),
	    "init" : NtpClient::chrootInit,
	    "store" : NtpClient::chrootStore,
	    "handle" : ntpEnabledOrDisabled,
	    "help" : HELPS["chroot_environment"]:"",
	],
	"server_address" : union (address, $[
	    // text entry label
	    "label" : _("&NTP Server"),
	    "help" : HELPS["server_address"]:"",
	    "handle" : serverAddressHandle,
	    "widget" : `custom,
	    "custom_widget" : `HBox (
		`ReplacePoint (`id (`server_address_rp),
		    `ComboBox (`id ("server_address"),
			`opt (`editable, `hstretch),
			// combo box label
			_("&NTP Server"))
		),
		`VBox (
		    `Label (" "),
		    // push button
		    `PushButton (`id (`lookup_address), _("&Lookup"))
		)
	    ),
	]),
	"overview" : $[
	    "widget" : `custom,
	    "custom_widget" : `VBox (
		`Table (`id (`overview), `opt (`notify), `header (
		    // table header
		    _("Source Type"),
		    // table header
		    _("Address"))),
		`HBox (
		    `PushButton (`id (`add), Label::AddButton ()),
		    `PushButton (`id (`edit), Label::EditButton ()),
		    `PushButton (`id (`delete), Label::DeleteButton ()),
		    `HStretch (),
		    // menu button
		    `MenuButton (`id (`advanced), _("&Advanced"), [
			// item of menu button
			`item (`id (`display_log), _("Display &Log")),
			// item of menu button
			`item (`id (`firewall), _("Set &Firewall"))
		    ])
		)
	    ),
	    "help" : HELPS["overview"]:"",
	    "init" : NtpClient::overviewInit,
	    "handle" : NtpClient::overviewHandle,
	],
	"paddress" : union (address, $[
	    "help" : HELPS["paddress"]:"",
	]),
	"bcaddress" : union (address, $[
	    "help" : HELPS["bcaddress"]:"",
	]),
	"bccaddress" : union (address, $[
	    "help" : HELPS["bccaddress"]:"",
	]),
	"clock_type" : $[
	    "widget" : `combobox,
	    // combo box label
	    "label" : _("Clock &Type"),
	    "items" : getClockTypesCombo (),
	    "help" : HELPS["clock_type"]:"",
	    "opt" : [ `notify ],
	    "init" : clockTypeInit,
	    "handle" : clockTypeHandle,
	    "store" : clockTypeStore,
	],
	"unit_number" : $[
	    "widget" : `intfield,
	    // int field
	    "label" : _("Unit &Number"),
	    "help" : HELPS["unit_number"]:"",
	    "minimum" : 0,
	    "maximum" : 3,
	    "init" : unitNumberInit,
	    "store" : unitNumberStore,
	],
	"create_symlink" : $[
	    "widget" : `checkbox,
	    // check box
	    "label" : _("Create &Symlink"),
	    "opt" : [ `notify ],
	    "init" : createSymlinkHandle,
	    "handle_events" : [ "create_symlink" ],
	    "init" : createSymlinkInit,
	    "handle" : createSymlinkHandle,
	    "store" : createSymlinkStore,
	    "help" : " ",
	],
	"device" : $[
	    "widget" : `textentry,
	    // text entry
	    "label" : _("&Device"),
	    "init" : deviceInit,
	    "store" : deviceStore,
	    "help" : HELPS["device"]:"",
	],
	"browse" : $[
	    "widget" : `push_button,
	    "label" : Label::BrowseButton (),
	    "handle_events" : [ "browse" ],
	    "handle" : browseButtonHandle,
	    "help" : " ",
	],
	"init_sync" : $[
	    "widget" : `checkbox,
	    // checkbox
	    "label" : _("Use for &Initial Synchronization"),
	    "init" : NtpClient::initSyncInit,
	    "store" : NtpClient::initSyncStore,
	    "help" : HELPS["init_sync"]:"",
	],
	"options" : $[
	    "widget" : `textentry,
	    // text entry label
	    "label" : _("O&ptions"),
	    "init" : NtpClient::optionsInit,
	    "store" : NtpClient::optionsStore,
	    "help" : HELPS["options"]:"",
	],
	"peer_types" : $[
	    "widget" : `radio_buttons,
	    "items" : [
		// radio button, NTP relationship type
                ["server", _("&Server")],
                // radio button, NTP relationship type
                ["peer", _("&Peer")],
                // radio button, NTP relationship type
                ["__clock", _("&Radio Clock")],
                // radio button, NTP relationship type
                ["broadcast", _("&Broadcasting")],
                // radio button, NTP relationship type
                ["broadcastclient", _("&Accepting Broadcast Packets")]
	    ],
	    // frame
	    "label" : _("Synchronization Peer Type"),
	    "init" : NtpClient::peerTypesInit,
	    "handle" : NtpClient::peerTypesHandle,
	    "help" : HELPS["peer_types"]:"",
	    "hspacing" : 3,
	    "vspacing" : 1,
	],
    ];
}



}
